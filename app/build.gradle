apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'svg'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.2"

    defaultConfig {
        applicationId "cn.cctech.kccommand"
        minSdkVersion 21
        targetSdkVersion 28
        multiDexEnabled true
        ndk {
            abiFilters 'armeabi'
        }
    }
    signingConfigs {
        release {
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
            storeFile file(KEY_STORE_FILE)
            storePassword KEY_STORE_PASSWORD
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }
    dataBinding {
        enabled = true
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation 'com.android.support:support-v13:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.orhanobut:logger:2.1.1'
    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
//    compile 'com.facebook.stetho:stetho:1.3.0'
//    compile 'com.facebook.stetho:stetho-okhttp3:1.3.0'
    implementation('com.github.hackware1993:MagicIndicator:1.4.0') {
        exclude group: 'com.android.support', module: 'support-v13'
        exclude group: 'com.android.support', module: 'appcompat-v7'
    }
    implementation('com.marshalchen.ultimaterecyclerview:library:0.7.3') {
        exclude group: 'com.android.support', module: 'support-v13'
        exclude group: 'com.android.support', module: 'appcompat-v7'
    }
    implementation('com.daimajia.numberprogressbar:library:1.4@aar') {
        exclude group: 'com.android.support', module: 'support-v13'
        exclude group: 'com.android.support', module: 'appcompat-v7'
    }
    implementation group: 'commons-httpclient', name: 'commons-httpclient', version: '3.1'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.1'
    implementation group: 'com.google.guava', name: 'guava', version: '23.3-android'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.pgyersdk:sdk:2.8.1'
    implementation 'com.github.megatronking:svg-support:1.3.2'
    implementation 'com.android.support:multidex:1.0.3'
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    implementation "io.reactivex.rxjava2:rxjava:2.1.9"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
}

def releaseNote = ""

task pgyer(type: Exec) {
    if (project.hasProperty('note')) {
        releaseNote = note
    }
    executable 'sh'
    args "-c", "./uploadApk.sh $releaseNote"
    dependsOn 'assembleRelease'
}

svg {
    // 下面四个参数是vector文件生成java渲染类文件的相关配置
    // vector资源文件目录，支持多个目录
    vectorDirs = ["src/main/svg_debug/drawable"]
    // shape资源文件目录
    shapeDir = "src/main/svg_release/drawable"
    // 生成java渲染类的存放包目录
    javaDir = "src/main/java/com/github/megatronking/svg/sample/drawables"

    // 下面参数是svg格式文件生成vector资源文件的配置
    svg2vector {
        // 这里的test可以随意命名，建议使用文件夹名
        test {
            // svg格式文件目录
            svgDir = "${projectDir}/svg"
            // 生成vector资源文件的存放目录
            vectorDir = "src/main/svg_debug/drawable"
            // 宽高参数指定生成vector文件的宽高，如果省略将默认使用图片原始宽高
            // 注意此处宽高比要与图片宽高比一致，否则图片会变形
//            height = 48
//            width = 48
        }
    }
}
